 OnRumbaRequest←{
     request←⍵
     ⎕←request.Path
     request.QueryParameters[;0]←(819⌶)¨request.QueryParameters[;0]         ⍝ query param names are not case sensitive
     response←#.CarlisleGroup.Rumba.Core.NewResponse request
     response.metadata←⎕NS''
     _←response LogRequestToS3&⍵
     0::response SendError ⎕DMX
     response.StatusCode←404
     request.Path≡'/favicon.ico':response GetFavIcon request
     0=≢request.Segments:response               ⍝ no path -> 404
     entry←(819⌶)0⊃request.Segments
     0=EndPoints.⎕NC entry:response             ⍝ invalid endpoint -> 404
     entryRef←EndPoints⍎entry
     validProps←entryRef.ValidRequestTypes
     vii←validProps[;0]⍳⊆request.Method
     vii=≢validProps:response SendInvalidMethod request              ⍝ HTTP method not implemented -> 501
     response.ContentType←entryRef GetResponseContentType request
     0=entryRef.⎕NC request.Method:'HTTP handler method not implemented.'⎕SIGNAL 11  ⍝ handler function missing?
     methodFoo←entryRef⍎request.Method
     response methodFoo request

    ⍝ (request checkMethod'GET')∧'html'≡0⊃request.Segments:response GetStatic request.Path
    ⍝ (request checkMethod'GET')∧'expression'≡(819⌶)0⊃request.Segments:response ProcessExpression request
    ⍝ (request checkMethod'GET')∧'jobs'≡(819⌶)0⊃request.Segments:response ProcessGetJobs request

      ⍝    (request checkMethod'GET')∧'expression'≡(819⌶)0⊃request.Segments:response ProcessExpressionAsync request
 ⍝    (request checkMethod'POST')∧'expression'≡(819⌶)0⊃request.Segments:response ProcessExpressionFunction request

     response
 }

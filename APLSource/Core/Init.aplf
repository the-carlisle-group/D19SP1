 {trapCode}Init dummy
 ;code
 ;settingsPath
 ;appSettingsPath
 ;msg

 ⍝ Set global ref to Core namespace
 ⎕SE._d19←⎕THIS

 ⍝ Optional left arg defines error trapping.  Default is none.
 :If 0=⎕NC'trapCode'
     trapCode←⍬
 :EndIf

⍝ Print ⎕DMX to session on any startup error
 ⎕TRAP←trapCode'C' 'ShowTrapInSession ⎕DMX'

 ⍝ Print session info
 ⎕←'⎕WSID: ',⎕WSID
 ⎕←'User: ',⎕AN
 ⎕←'⎕WA: ',⍕⎕WA
 ⎕←'APLVersion: ',⍕Utils.APLVersion

 ⍝ Setup namespace refs to dependency projects
 ⎕SE._rumba←##.##.CarlisleGroup.Rumba
 ⎕SE._dcl←##.##.DCL

 ⎕←(⎕UCS 13 10),'Starting...',⎕UCS 13 10

 ⍝ Reset any existing Rumba/Conga connection.
 ResetRumbaConga''

⍝ Initilize project and dependency project
⍝ ./AcreConfig/ProjectFolder paths.
⍝ Required for each project to have access to assets.
 InitProjectFolders''

 ⍝ Init Dyalog Crypto Library.
 code←⎕SE._dcl.(Crypt.Init AcreConfig.ProjectFolder,'Assets/Runtime/')

 :If 0≠code
     ⎕SIGNAL⊂('EN' 11)('EM' 'DCL library could not be initialized.')('Message'('DCL Error: ',⍕code))
 :EndIf

 ⍝ Initialize conga dll and class
 ⎕SE._rumba.Core.InitConga''

 ⍝ Verify conga is accessible by print Conga DLL UTC time
 ⎕←'Conga TimeStamp: ',1⊃⎕SE._rumba.DRC.GetProp'.' 'HttpDate'

 ⍝ _settings.json file path.
 settingsPath←##.AcreConfig.ProjectFolder,'settings.json'

 ⍝ Is settings.json file available
 :If ~⎕NEXISTS settingsPath
     ⎕SIGNAL⊂('EN' 22)('EM' 'File settings.json is missing')('Message' 'Did you create a settings file from the settings-template.json file?')
 :EndIf

 ⍝ Read in settings file.
 _settings←⎕JSON 0⊃⎕NGET settingsPath

 ⍝ Set _settings.hostname.  Will be used when more than one
 ⍝ server is hosting requests.
 _settings.hostName←(∊⎕CMD'hostname')~' '

 :Section rumbaInit
     ⍝ Should dyalog listen for ride connections?
     ⍝useRide←_settings.RideEnabled≡⊂'true'
     ⍝useRide←useRide∧'W'≢↑↑v

     ⍝ RIDE only on non linux right now
     ⍝⎕←useRide/'RIDE Server enabled on port ',⍕_settings.RidePort
     ⍝ok←useRide{
     ⍝    r←3502⌶0
     ⍝    r←3502⌶'SERVE::',⍕⍵ ⍝ Update init string
     ⍝    3502⌶⍺
     ⍝}_settings.RidePort
 :EndSection

 ⍝ _settings.json file path.
 appSettingsPath←##.AcreConfig.ProjectFolder,'settings-apps.json'

 ⍝ Is settings.json file available
 :If ~⎕NEXISTS appSettingsPath
     ⎕SIGNAL⊂('EN' 22)('EM' 'File settings.json is missing')('Message' 'Did you create a settings-apps.json file?')
 :EndIf

 _settingsApps←⎕JSON 0⊃⎕NGET appSettingsPath

 ⍝ Check to make sure atleast one app is defined.
 :If 0=≢_settingsApps.⎕NL ¯9
     ⎕SIGNAL⊂('EN' 11)('EM' 'The settings-apps.json file contains no object keys defined.')
 :EndIf

 ⍝ fix app ref string to namespace ref
 :Trap 6
     _settingsApps.(ref←⍎ref)
 :Else
     msg←'Error in definition(S): ',¯2↓∊((0=⎕NC _settingsApps.ref)/_settingsApps.name),⊂', '
     ⎕SIGNAL⊂('EN' 11)('EM' 'Invalid ref in settings-apps.json file.')('Message'msg)
 :EndTrap

 ⍝ Verify all endpoints have correct min required
 ⍝ variables and functions
 ValidateAppEndPointStructure¨_settingsApps

 ⍝ Execute initapp function for each app in settings
 _settingsApps.ref.InitApp ⎕THIS

 ⍝ What certificate paths will be used to
 ⍝ init the conga server?  Print the paths.
 ⎕←'Cert Path: ',##.AcreConfig.ProjectFolder,_settings.SSLCertPath
 ⎕←'Cert Key: ',##.AcreConfig.ProjectFolder,_settings.SSLKeyPath

 ⍝ User must be root (admin) to bind port below 1025 on linux.
 ⍝ Check...
 ⍝ if not Windows
 ⍝ if not the root user
 ⍝ if not port above 1024
 ⍝ Throw an error if we think the process wont start.
 ⎕←((⎕AN≢'root')∧('W'≢↑↑Utils.APLVersion)∧_settings.Port<1025)/'WARNING!!! Unix process will not start the server on port below 1024 without root privileges.'

 ⍝ Init user access rights table
 ⍝ Runs in a thread and refhresh the _accessTable at interval
 ⍝ set by settings.json -> _settings.AuthTableRefresh in seconds
 ⍝ Done after project S3 bucket is already created.
 :If _settings.UseAuth≡⊂'true'
     Auth.CreateAccessTableNotExist''
     Auth.InitAccessTableRefresh&''
 :EndIf

 ⍝ Start the secure REST API Rumba server.
 ⍝ Create a global ref to the server.
 _server∘←StartServer''

 :Section post startup session dump
                ⍝ ↓↓↓↓↓↓
     ⍝ Create resources to make distribution easier.
     ⍝ TBD
     ⎕←''
     ⍝     path←'https://localhost:',(⍕_settings.Port),'/expression/',rumba.Core.PercentEncode'({Utils.ToLower }@(⍸(2|⍸(≢⎕A)/1)))⎕A'
     ⎕←'Running: ','https://localhost/',((0⊃_settingsApps).name),'/html/index.html'
     →('CLEAR WS'≡⎕WSID)/0
     ⎕DQ'.'
     _←⎕SE.UCMD'open ','https://localhost/',((0⊃_settingsApps).name),'/html/index.html'

    ⍝ docker startup with hostname
    ⍝ docker run -h thisismyhostname -v c:/temp:/data dyalog/dyalog /data/dockertest.dws

 :EndSection
